10 REM  REAL-TIME CLOCK (RTC) DEMO
30 REM  by Bruce E. Hall, W8BH.net
40 REM  on 11 Aug 2025
42 REM 
50 REM  Lines 100-199: get time variables
60 REM  Lines 200-299: print the time
70 REM  Lines 300-399: print the day of the week 
80 REM  Lines 400-499: print the date 
90 REM  Lines 500-599: subroutine for printing 2-digit values
99 REM  
100 OUT 65,0: SEC  = BIN(IN(66))
110 OUT 65,1: MNUT = BIN(IN(66))
120 OUT 65,2: HOUR = BIN(IN(66))
130 OUT 65,3: D1   = IN(66)
140 OUT 65,4: D2   = IN(66)
150 OUT 65,5: DAY  = BIN(IN(66))
160 OUT 65,6: MNTH = BIN(IN(66))+1
170 OUT 65,7: YEAR = BIN(IN(66)-160)
180 DAYS = D2*256 + D1 
190 DOW = (DAYS-1) MOD 7 
200 PRINT "The time is ";
210 HR$=" AM": IF HOUR<12 THEN 240
220 HR$=" PM"
230 IF HOUR>12 THEN HOUR = HOUR-12
240 A=HOUR: GOSUB 500: PRINT ":";
250 A=MNUT: GOSUB 500: PRINT ":";
260 A=SEC:  GOSUB 500: PRINT HR$;
270 PRINT ", ";
300 DOW$="SunMonTueWedThuFriSat"
310 PRINT MID$(DOW$,1+DOW*3,3);" ";
400 A=MNTH: GOSUB 500: PRINT "/";
410 A=DAY:  GOSUB 500: PRINT "/";
420 A=YEAR: GOSUB 500
499 END
500 REM PRINT 2-DIGIT VALUE IN A 
510 IF A<10 THEN PRINT "0";
520 PRINT MID$(STR$(A),2);
530 RETURN