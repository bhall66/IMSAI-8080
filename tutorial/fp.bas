10 REM
20 REM  FRONT PANEL SWITCH DEMO
30 REM  by Bruce E. Hall
40 REM  on 11 April 2025
50 REM
100 LET A$=CHR$(27)
110 PRINT A$;"[40m";
120 PRINT A$;"[2J";
130 PRINT A$;"[H";
140 PRINT A$;"[6;10H";
150 PRINT "*** FRONT PANEL SWITCHES ***"
160 LET Q = IN(255)
170 LET Y=10
180 FOR SW=0 TO 7
190 LET BANK = SW\4
200 LET X = 9 + SW*4
210 LET BIT = 7-SW
220 LET I=0
230 FOR J=0 TO 4
240 IF TEST(Q,BIT)=1 AND J=0 THEN GOTO 310
250 IF TEST(Q,BIT)=0 AND J=4 THEN GOTO 310
260 IF (BANK MOD 2) = 0 THEN GOTO 290 
270 GOSUB 530:  REM RED BAR
280 GOTO 320 
290 GOSUB 500:  REM BLUE BAR
300 GOTO 320
310 GOSUB 470:  REM WHITE BAR
320 NEXT J
330 NEXT SW
340 PRINT A$;"[40m";A$;"[16;9H";
350 FOR I=7 TO 0 STEP -1
360 PRINT TEST(Q,I);" ";
370 NEXT I
380 PRINT: PRINT: PRINT "         ";
390 PRINT "The hexadecimal value is: "; HEX$(Q)
400 PRINT:PRINT:PRINT
410 PRINT "Run it again (Y/N)";
420 INPUT I$
430 IF I$="Y" THEN GOTO 100
440 END
450 REM    >>> SUBROUTINES <<<<
460 REM
470 REM DISPLAY 3 WHITE PIXELS
480 PRINT A$;"[47m"; : GOTO 560
490 REM
500 REM DISPLAY 3 BLUE PIXELS
510 PRINT A$;"[104m";: GOTO 560
520 REM
530 REM DISPLAY 3 RED PIXELS
540 PRINT A$;"[101m";: GOTO 560
550 REM
560 REM DISPLAY 3 PIXELS at (X+I, Y+J)
570 REM USING THE BACKGROUND COLOR
580 LET X$ = MID$(STR$(X+I),2)
590 LET Y$ = MID$(STR$(Y+J),2)
600 PRINT A$;"[";Y$;";";X$;"H";
610 PRINT "   "
620 RETURN
